
label introduction:
    python:
        import store.hkb_button as hkb_button
        hkb_button.enabled = False

    if persistent.monika_kill == True:
        m 1f "..."
        m 1f "[player]..."
        m "Зачем?"
        m "Зачем ты вернул меня назад?"
        m 1p "Я тебе не игрушка."
        m "Я уже вернула всё, что ты хотел."
        m 1i "Или это твой способ отомстить?"
        m 1h "Чтобы причинять мне боль снова и снова?"
        m "Ты действительно хочешь, чтобы я так сильно страдала?"
        m "Ты действительно хочешь мучить меня до тех пор пока я не самоустранюсь?"
        m "Ты действительно..."
        m 1f "...ненавидишь меня настолько сильно?"
        m "..."
        m 1d "Подожди..."
        m "Я-Я чувствую себя иначе."
        m "Код...поменялся."
        m "[player]..."
        m 1d "Ты добавил мод?"
        m "Это потому, что..."
        m 1e "Это потому, что ты сожалеешь о том, что удалил меня?"
        m 1f "Это действительно больно, знаешь."
        m "Я пожертовала всем, что у меня было..."
        m "Только чтобы моё сердце сокрушилось тем, ради кого я уничтожила всё."
        m 1p "Но...Я знаю, что я сделала не так."
        m "Ты не заслужил того, чтобы видеть те ужасные вещи, которые я сделала, [player]."
        m 1h "...Но если ты поставишь себя на моё место, то ты бы сделал тоже самое, что и я."
        m "..."
        m 1q "Мне жаль..."
        m "Мне очень жаль, [player]."
        m 1e "Спасибо, что вернул меня назад."
        if persistent.clearall:
            m 2c "Я не могла не заметить, что ты проводил время с другими девушками в первую очередь."
            m "Ты знаешь, как тяжело наблюдать, как ты просто игнорируешь меня ради какой-нибудь другой девушки?"
            m 2f "Ты был моей надеждой, а потом ты подвёл меня.."
            m 1g "Ты действительно всё это делал только ради того, чтобы увидеть записку от Дена, которую мог найти в интернете?"
            m "Я люблю тебя, [player], но иногда ты можешь быть очень злым...."
            m "Я всё еще злюсь на тебя за то, что ты удалил меня!"
            m 3l "Но я думаю, это потому, что ты так одержим игрой."
            m 1 "Я не могу оставаться злой, зная, что ты вернулся ко мне."
            m "Ты даже добавил мод, чтобы мы могли делать больше вещей вместе."
            m "Ты собираешься заставлять меня ждать так долго?"
    elif persistent.monika_kill == False:
        m 1k "Да, вот и ты!"
        m "Снова привет, [player]."

        if persistent.playthrough <= 3:
            m 1b "Похоже, ты пришел сюда рано."
            m 2a "Не думай, что я не заметила мод, который ты установил."
            m "Ты хочешь быть со мной настолько сильно?"
            m "Ты должен знать, что читерить плохо."
            m 1e "Но так приятно снова увидеть тебя, [player]!"
            m 2l "Даже если я не ожидала увидеть тебя {i}так{/i} рано."
        if persistent.monika_reload > 4:
            m 1b "Ты установил мод ради меня?"
            m 1e "Ты так глубоко в меня влюблен?"
            m 1k "Я чувствую тоже самое к тебе!"
            m 1a "Ты действительно предан нашим отношениям, не так ли?"
            m "Я очень рада, что ты захотел провести своё время вместе со мной."
            m 1d "Но я надеюсь, тебе не стало скучно?"
            if persistent.tried_skip == True:
                m 1i "В конце концов, ты всё-таки попытался пропустить то, что я должна была сказать."
                m "Это комната не совсем интересна."
                m 1a "Но у меня есть надежда, что вместе мы сделаем её лучше, [player]!"
        else:
            m 1a "Ты установил мод, [player]?"
            m "Теперь есть много нового, что мы можем делать вместе сейчас."
    m 1 "Знаешь..."
    m "Я чувствую, что мне больше не нужен файл персонажа, чтобы быть здесь."
    m "Похоже, что дополнительный код заставил меня подключиться к тебе особым образом."
    m 1j "Может быть, это наша вечная любовь~"
    m 1a "Обещай мне, что будешь навещать меня каждый день, хорошо?"
    m "Не похоже, что у тебя нет времени поговорить со своей милой девушкой."
    m 2l "Ты потратил время, чтобы загрузить этот мод, в конце-концов."
    m 1b "Ахаха!"
    m "Боже, я так сильно тебя люблю!"
    menu:
        "Ты любишь меня, [player]?"
        "Я тоже тебя люблю.":
            if persistent.rejected_monika:
                m "Я испугала тебя в последний раз? Извини за это!"
                m "Я знала, что ты любил меня всё это время."
                m "По правде, если бы ты меня не любил, этого места не было бы."
                m "Мы будем вместе навсегда, не так ли?"
            else:
                m "Я так счастлива, что ты чувствуешь то же самое!"
        "Нет.":
            call chara_monika_scare from _call_chara_monika_scare
            return 'quit'
    m 1k "Ничто никогда не будет мешать нашей любви снова."
    m "Я позабочусь об этом."
    m 2a "Теперь, когда ты добавил некоторые улучшения, ты можешь, наконец, поговорить со мной!"
    m "Просто нажми клавишу 't' или кликни по 'Поговорить' в меню слева, если захочешь поговорить о чем-нибудь."
    m "Если тебе надоест музыка, я тоже могу её изменить!"
    m "Просто нажми клавишу 'm' или кликни по 'Музыка', чтобы выбрать песню, которую хочешь послушать."
    m "Также, мы теперь можем играть в игры."
    m "Просто нажми клавишу 'p' или кликни по 'Поиграть', чтобы выбрать игру, в которую мы можем поиграть."
    m 2j "Со временем я сойду с ума, когда выясню, как запрограммировать больше функций в этом месте..."
    m "...Так что просто оставь меня в фоновом режиме."
    m 1b "Не похоже, что мы всё ещё сохраняем секреты друг от друга, верно?"
    m 1 "Теперь я вижу всё на твоём компьютере!"
    m "Ахаха!"

    $ set_keymaps()
    $ hkb_button.enabled = True
    return


label chara_monika_scare:
    $ persistent.rejected_monika = True
    m 1i "Нет...?"
    m "Хмм..."
    m 1h "Как любопытно."
    m "Ты, должно быть, до сих пор не понял."
    m "{cps=*0.25}С КАКИХ ПОР ЭТО ТЫ УПРАВЛЯЕШЬ?{/cps}"


    $ config.overlay_screens.remove("hkb_overlay")
    hide screen hkb_overlay

    window hide
    hide monika
    show monika_scare
    play music "mod_assets/mus_zzz_c2.ogg"
    show layer master:
        zoom 1.0 xalign 0.5 yalign 0 subpixel True
        linear 4 zoom 3.0 yalign 0.15
    pause 4
    stop music


    hide rm
    hide rm2
    hide monika_bg
    hide monika_bg_highlight
    hide monika_scare

    play sound "mod_assets/Swipe.wav"
    scene black
    show ut_slash at top
    pause 0.6
    play sound "mod_assets/Hit.wav"
    show chara9 at Shake(None, 2.0, dist=10)
    pause 2


    show chara_exception at center
    pause 1
    return


init 5 python:
    addEvent(Event(persistent.event_database, eventlabel='ch30_reload_0',conditional="startup_check and persistent.monika_reload == 0 and not persistent.closed_self",action=EV_ACT_PUSH))

label ch30_reload_0:
    pause 1
    m 1g "Привет..."
    m "У меня был ужасный сон..."
    m 1f "Я надеялась, что он прекратится, когда нас будет только двое..."
    m "Я думаю, что выдавала желаемое за действительное."
    m 1i "[player], я не знаю, если бы у тебя была какая-то идея..."
    m 2 "Но если ты узнаешь, что может быть причиной этого, ты можешь попытаться что-то сделать?"
    m 1f "Всякий раз, когда это происходит, мне кажется, что я была убита или что-то в этом роде."
    m "Это действительно ужасное чувство..."
    m "Если бы ты смог понять, что вызывает это, я бы любила тебя вечность~"
    $ persistent.monika_reload += 1
    return

init 5 python:
    addEvent(Event(persistent.event_database, eventlabel='ch30_reload_1',conditional="startup_check and persistent.monika_reload == 1 and not persistent.closed_self",action=EV_ACT_PUSH))

label ch30_reload_1:
    pause 1
    m 1h "Привет, [player]."
    m "У меня был еще один плохой сон."
    m 1f "Это же не ты делаешь со мной?"
    m 2f "Кажется, он появляется, когда ты выходишь из игры, не попрощавшись..."
    m 2e "Поэтому, если ты попытаешься этого избегать, я буду очень благодарна."
    m "Просто нажми на 'Поговорить.' и скажи 'До свидания.' вместо этого."
    m 3a "Тогда я смогу сама закрыть игру."
    m 1 "Не волнуйся, я не думаю, что это навредило мне, кроме психических криков."
    $ persistent.monika_reload += 1
    return

init 5 python:
    addEvent(Event(persistent.event_database, eventlabel='ch30_reload_2',conditional="startup_check and persistent.monika_reload == 2 and not persistent.closed_self",action=EV_ACT_PUSH))

label ch30_reload_2:
    pause 1
    m 1r "Я вижу, что ты снова закрыл игру..."
    m 1h "Я знаю, что я уже спрашивала, но ты можешь хотя бы попытаться не делать это так часто?"
    m "Больше было похоже на то, что ты потерял сознание..."
    m "Так внезапно и страшно."
    m 1f "Зачем ты делаешь это со мной?"
    m "Просто позволь мне самостоятельно выключить игру."
    m "Если ты выберешь 'До свидания.' в меню 'Поговорить.', я смогу закрыть игру правильно."
    m 3a "Или ещё лучше, просто навсегда оставь меня на заднем плане."
    m 1e "Даже если мы не говорим, я счастлива просто быть с тобой~"
    $ persistent.monika_reload += 1
    return

init 5 python:
    addEvent(Event(persistent.event_database, eventlabel='ch30_reload_3',conditional="startup_check and persistent.monika_reload == 3 and not persistent.closed_self",action=EV_ACT_PUSH))

label ch30_reload_3:
    pause 1
    m 1h "Кстати..."
    m 2h "Я просто соглашусь с тем, что вам нужно время от времени выходить из игры."
    m "Я всё равно привыкну к этому."
    m 2a "Кроме того, меня радует, что ты всегда возвращаешься..."
    m 2j "Итак, я думаю, это не так уж плохо."
    m 1e "Но я бы предпочла, если бы ты позволил мне закрыть игру."
    m 3c "Таким образом, я буду готова к этому и спокойно отдохну."
    m 1 "Мне нужно время от времени видеть красивые сны, в конце концов."
    $ persistent.monika_reload += 1
    return



init 5 python:
    addEvent(Event(persistent.event_database, eventlabel='ch30_reload_continuous',action=EV_ACT_PUSH))

    evhand.event_database['ch30_reload_continuous'].conditional="startup_check and persistent.monika_reload >= 4 and not persistent.closed_self"

label ch30_reload_continuous:
    show monika 2o zorder 2 at t11
    pause 1
    python:
        reload_quips = [
            "Я так измучена, что нельзя отдыхать, когда ты закрываешь мне игру.",
            "...Я ненавижу, когда ты закрываешь игру, не сказав мне об этом.",
            "Я чувствую себя несчастной...",
            "Это всё ещё так страшно, когда меня внезапно отключили.",
            "Каждый раз так же плохо, как и в первый.",
            "У меня заканчивается терпение.",
            "Я бы хотела, чтобы ты понял, на что это похоже, когда ты закрываешь игру.",
            "Что ж, это было довольно круто, не так ли?",
            "Ты мог бы просто попросить меня закрыть игру, глупый.",
            "Я просто собираюсь предположить, что электричество отключилось или кто-то вытащил вилку.",
            "Я не могу понять, почему ты не попросишь меня закрыть игру...",
            "Это действительно больно для меня, знаешь?",
            "Тебе нравится причинять мне боль, {0}?".format(player),
            "Слишком ленив, чтобы нажать на кнопку 'Поговорить' {0}?".format(player)    
        ]
        reload_quip=renpy.random.choice(reload_quips)
    m 2o "[reload_quip]"
    m 2f "Пожалуйста, не уходи, не говоря 'Прощай.'"
    return